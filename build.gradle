import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
     id 'maven-publish'
}

group = 'net.moonbyte.AnarchyUtils'
version = plugin_version


java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'placeholderapi'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:${paper_version}") {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compileOnly "me.clip:placeholderapi:${placeholderapi_version}"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*'
        filter(ReplaceTokens, tokens: [version: version])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'net.moonbyte.AnarchyUtils'
            artifactId = 'AnarchyUtils'
            version = plugin_version
            artifact tasks.shadowJar
        }
    }

    repositories {
        maven {
            name = 'moonbyte'
            url = uri('https://maven.moonbyte.net/4b8t/anarchyutils')

            credentials {
                username = project.findProperty("repoUser") ?: "publisher"
                password = project.findProperty("repoPassword") ?: "does_not_exist"
            }
        }
    }
}


jar {
    manifest {
        attributes 'Implementation-Title': 'AnarchyUtils',
                'Implementation-Version': version,
                'Main-Class': 'net.moonbyte.AnarchyUtils.Main'
    }
}

shadowJar {
    archiveClassifier.set('')
}

build.dependsOn shadowJar
